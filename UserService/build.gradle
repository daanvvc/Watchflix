plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.3'
    id "org.sonarqube" version "6.0.1.5171"
    id 'jacoco'
}

group = 'fontys.IA'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://artifactory-oss.prod.netflix.net/artifactory/maven-oss-candidates'
        content {
            excludeModule('org.springframework.cloud', 'spring-cloud-dependencies')
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2024.0.1"
    }
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    // Database
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.postgresql:postgresql:42.7.5'
    implementation 'org.flywaydb:flyway-core:9.16.3'

    // RabbitMQ
    implementation('org.springframework.boot:spring-boot-starter-amqp')

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-bootstrap:4.0.4'

    // Eureka
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:4.2.1'

    // Force update for security reasons
    implementation 'com.thoughtworks.xstream:xstream:1.4.21'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.4.3'
    implementation 'org.springframework.security:spring-security-crypto:6.4.5'
    implementation 'org.apache.commons:commons-configuration2:2.10.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
    }
}

jacoco {
    toolVersion = "0.8.12"
}

sonar {
    properties {
        property "sonar.projectKey", "Watchflix_UserService"
        property "sonar.organization", "daanvvc"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
    }
}